/* Creation of the initial mesh, level set function and velocity field */
include "./sources/inout.idp"
include "./sources/macros.idp"
load "tetgen"
load "msh3"

/* Get mesh and sol names */
string MESH = getsParam(EXCHFILE,"MeshName");
string PHI  = getsParam(EXCHFILE,"PhiName");
string VEL  = getsParam(EXCHFILE,"SolName");

/* Create the mesh of the faces */
int N = 50;
border a(t=0.0,1.0) {x=0.0; y=1.0-t; label=0;};
border b(t=0.0,1.0) {x=t; y=0.0; label=0;};
border c(t=0.0,1.0) {x=1.0; y=t; label=0;};
border d(t=0.0,1.0) {x=1.0-t; y=1.0; label=0;};

mesh  Th2d = buildmesh(a(N)+b(N)+c(N)+d(N));
meshS Tht   = movemesh23(Th2d,transfo=[x,y,1.0]);
meshS Thb   = movemesh23(Th2d,transfo=[x,1.0-y,0.0]);
meshS Thl1  = movemesh23(Th2d,transfo=[x,0.0,y]);
meshS Thl2  = movemesh23(Th2d,transfo=[1.0,x,y]);
meshS Thl3  = movemesh23(Th2d,transfo=[1.0-x,1.0,y]);
meshS Thl4  = movemesh23(Th2d,transfo=[0.0,1.0-x,y]);

meshS ThS   = Tht + Thb + Thl1 + Thl2 + Thl3 + Thl4;

/* Creation of the 3d mesh */
real[int] dominfo = [0.1,0.1,0.1,0,5e-6];
mesh3 Th3 = tetg(ThS,switch="pqaAAYYQ",nbofregions=1,regionlist=dominfo);

/* Create a level set function on the mesh */
fespace Vh(Th3,P1);
Vh phi,gx,gy,gz;

phi = (x-0.5)^2 + (y-0.5)^2 + (z-0.5)^2 - 0.2^2;

/* Create a velocity field on the mesh */
gx = x-0.5;
gy = y-0.5;
gz = z-0.5;

/* Save files */
savemesh(Th3,MESH);
printsol(PHI,phi[]);
printvec3(VEL,gx[],gy[],gz[]);
